# Class 4 - Interfaces and External calls

## External Contracts, Interfaces, Fallback Functions
```
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.7.0 <0.9.0;

// Interface used by Greeter to talk to HelloWorld contract
interface IHelloWorld {
    function helloWorld() external view returns (string memory);

    function setText(string memory newText) external;
}

// Base smart contract
contract HelloWorld {
    string private text;

    constructor() {
        text = pureText();
    }

    function helloWorld() public view returns (string memory) {
        return text;
    }

    function setText(string calldata newText) public {
        text = newText;
    }

    function pureText() public pure virtual returns (string memory) {
        return "Hello World";
    } // For deriving contract to override fn, it has to be virtual
}

// Part 0. Inheriting HelloWorld
contract HelloThere is HelloWorld {
    //Overrides pureText function
    function pureText() public pure override returns (string memory) {
        return "Hello There";
    }
}

// Part 1. External contract - we want to call HelloWorld from here, using the address
// For this we create an interface, because passing the address through target.call is complicated
contract Greeter {
    function invokeGreeting(address target)
        external
        view
        returns (string memory)
    {
        // return target.call();

        // Simple external call, where we directly call the contract
        // return HelloWorld(target).helloWorld();

        //Using the interface to call the external Contracts's function
        return IHelloWorld(target).helloWorld();

        // Internally it uses a function selecter. TODO- read on selector
    }

    // Part 2. Call setText using interface
    function setGreeting(address target, string calldata newText) external {
        IHelloWorld(target).setText(newText);
    }

    //Steps:
    // 1. Deploy HelloWorld, get address
    // 2. Deploy Greeter, pass addr of HelloWorld to invokeGreeting to interact
}

// Part 3. Incomplete implementation of IHelloWorld, missing setText
contract IncompleteHelloWorld {
    string private text;

    constructor() {
        text = pureText();
    }

    function helloWorld() public view returns (string memory) {
        return text;
    }

    function pureText() public pure virtual returns (string memory) {
        return "Hello World";
    }
    // 1. Deploy IncompleteHelloWorld, get address
    // 2. Deploy Greeter, pass addr of IncompleteHelloWorld to invokeGreeting to interact
    // 3. We can still call invokeGreeting (since helloWorld required by invokeGreeting
    // is implemented by IncompleteHelloWorld, although setText is not)
    // 4. setGreeting fails
}

// Part 4. Fallback function

/*
A contract can have at most one fallback function, declared using 
either fallback () external [payable] or 
fallback (bytes calldata input) external [payable] returns (bytes memory output) 
(both without the function keyword).
 This function must have external visibility.
 A fallback function can be virtual, can override and can have modifiers.
 Only works for payable and non-payable contracts

Its called on a call if none of the other functions in the contract have the matching function signature

*/
contract FallbackHelloWorld {
    string private text;

    constructor() {
        text = pureText();
    }

    function helloWorld() public view returns (string memory) {
        return text;
    }

    function pureText() public pure virtual returns (string memory) {
        return "Hello World";
    }

    fallback() external {
        text = "FALLBACK";
    }

    //Steps:
    // 1. Deploy FallbackHelloWorld, copy address
    // 2. Run Greeter > invokeGreeting at FallbackHelloWorld's address. Works because 
    // FallbackHelloWorld implements helloWorld
    // 3. Run Greeter > setGreeting with FallbackHelloWorld's id. Now we get the fallback msg instead
    // of the call failing
}

```

## Restricting Access:
```
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.7.0 <0.9.0;

contract HelloWorld {
    string private text;
    address public owner; // state variable to store the current owner

    /// Modifier checks if the owner is the same as the sender
    modifier onlyOwner()
    {
        require (msg.sender == owner, "Caller is not the owner");
        _; // Function to which modifier is attached, runs from here
    }

    modifier onlyOwnerIncorrect()
    {
        _; 
        // If we merge the function before, and try to transfer ownership to others from our account,
        // it fails, because ownership is changed, but then the whole txn is reverted coz require fails and reverts
        // but if we transfer it to our own account, the ownership changes, and the require statement evaluates to true
        // since I'm the owner as well as the sender. Hence this is incorrect
        require (msg.sender == owner, "Caller is not the owner");
    }

    constructor() {
        text = "Hello World";
        owner = msg.sender; // When the contract is deployed, owner is set to the sender
    }

    function helloWorld() public view returns (string memory) {
        return text;
    }

    /// Restricted function to change text, the modifier ensures only the current owner can run this function
    function setText(string calldata newText) public onlyOwner {
        text = newText;
    }

    /// Restricted function to change the ownership, the modifier ensures only the current owner can run this function
    function transferOwnership(address newOwner) public onlyOwner {
        owner = newOwner;
    }


}
```